#!/usr/bin/env python3
# Copyright 2024 K. D. Murray
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.

import argparse
import re
from collections import defaultdict
from pathlib import Path
from snakemake.api import SnakemakeApi
import json
from sys import stderr

SNAKEFILE=r"""
configfile: "config.json"

def all_inputs(wc):
    res = ["output/multiqc.html",]
    for sample in config["samples"]:
        if config.get("reference"):
            res.append(f"tmp/aln/{sample}.bam")
        if config.get("kraken_db"):
            res.append(f"output/taxonid/kraken_report/{sample}.txt")
    if config.get("mash"):
        res.append("output/mash.dist.tsv")
    return res

rule all:
    input: all_inputs


rule subsample:
    input:
        r1=lambda wc: config["samples"][wc.sample]["R1"],
        r2=lambda wc: config["samples"][wc.sample]["R2"],
    output:
        r1=temp("tmp/reads/sub/{sample}_subsample_R1.fastq"),
        r2=temp("tmp/reads/sub/{sample}_subsample_R2.fastq"),
    log:
        "tmp/reads/sub/{sample}_subsample.log",
    threads: 1
    container: "docker://quay.io/mbhall88/rasusa"
    params:
        seed=config.get("subsample_seed", 2123),
        nreads=config.get("subsample", 0),
    shell:
        "( rasusa reads"
        "   --num {params.nreads}"
        "   --seed {params.seed}"
        "   -o {output.r1} -o {output.r2}"
        "   {input.r1} {input.r2}"
        ") 2>{log}"


rule head:
    input:
        lambda wc: config["samples"][wc.sample][wc.read],
    output:
        temp("tmp/reads/sub/{sample}_head_{read}.fastq"),
    log:
        "tmp/reads/sub/{sample}_head_{read}.log",
    threads: 1
    params:
        n=config.get("head", 0)*4,
    shell:
        "set +o pipefail; zcat {input} | head -n {params.n} > {output} 2>{log} "


def fastp_input(wc):
    if config["subsample"] > 0:
        return {
            "r1": f"tmp/reads/sub/{wc.sample}_subsample_R1.fastq",
            "r2": f"tmp/reads/sub/{wc.sample}_subsample_R2.fastq",
        }
    elif config["head"] > 0:
        return {
            "r1": f"tmp/reads/sub/{wc.sample}_head_R1.fastq",
            "r2": f"tmp/reads/sub/{wc.sample}_head_R2.fastq",
        }
    else:
        return {
            "r1": config["samples"][wc.sample]["R1"],
            "r2": config["samples"][wc.sample]["R2"],
        }
rule fastp:
    input:
        unpack(fastp_input)
    output:
        r1=temp("tmp/reads/qc/{sample}_qc_R1.fastq"),
        r2=temp("tmp/reads/qc/{sample}_qc_R2.fastq"),
        rs=temp("tmp/reads/qc/{sample}_qc_SE.fastq"),
        json="output/log/fastp/{sample}.json",
        html="output/log/fastp/{sample}.html",
    log:
        "tmp/reads/qc/{sample}_qc.log",
    threads: 2
    container: "docker://quay.io/biocontainers/fastp:0.23.4--hadf994f_3"
    shell:
        "fastp "
        "   --in1 {input.r1}"
        "   --in2 {input.r2}"
        "   --out1 {output.r1}"
        "   --out2 {output.r2}"
        "   --unpaired1 {output.rs}"
        "   --unpaired2 {output.rs}"
        "   --thread {threads}"
        "   --json {output.json}"
        "   --html {output.html}"
        "   --low_complexity_filter"
        "   --detect_adapter_for_pe"
        "   --cut_tail"
        "   --trim_poly_x"
        ">{log} 2>&1"

rule kraken:
    input:
        r1="tmp/reads/qc/{sample}_qc_R1.fastq",
        r2="tmp/reads/qc/{sample}_qc_R2.fastq",
    output:
        report="output/taxonid/kraken_report/{sample}.txt",
    log:
        "output/log/kraken/{sample}.log",
    resources:
        runtime=90,
        mem="100G",
    threads: 16
    container: "docker://ghcr.io/kdm9/kraken2:latest"
    params:
        db=config.get("kraken_db"),
    shell:
        "kraken2"
        "   --db {params.db}"
        "   --memory-mapping"
        "   --threads {threads}"
        "   --use-names"
        "   --report-minimizer-data"
        "   --report {output.report}"
        "   --paired"
        "   {input.r1} {input.r2}"
        "   >/dev/null 2>{log}"

rule idxref:
    input:
        ref=lambda wc: config.get("reference", []),
    output:
        idx=temp("tmp/ref.mmi"),
    log:
        "tmp/ref.mmi.log",
    threads: 4
    container: "docker://ghcr.io/kdm9/minimap2-samtools:latest"
    shell:
        "minimap2 -x sr -d {output.idx} -t {threads} {input.ref} &>{log}"

rule map:
    input:
        r1="tmp/reads/qc/{sample}_qc_R1.fastq",
        r2="tmp/reads/qc/{sample}_qc_R2.fastq",
        idx="tmp/ref.mmi",
    output:
        bam="tmp/aln/{sample}.bam",
        bai="tmp/aln/{sample}.bam.bai",
        minimap="output/log/minimap/{sample}.log",
        markdup="output/log/markdup/{sample}.log",
    log:
        log="tmp/aln/{sample}.log",
    container: "docker://ghcr.io/kdm9/minimap2-samtools:latest"
    threads: 8
    resources:
        mem="16G"
    params:
        mem=lambda wc, input, output, resources: int((resources.mem_mb*0.4)/resources._cores),
    shell:
        "( minimap2 -ax sr"
        "   -t {threads}"
        "   {input.idx}"
        "   {input.r1} {input.r2}"
        "   2>{output.minimap}"
        " | samtools fixmate "
        "   -m"
        "   -@ {threads}"
        "   -u"
        "   /dev/stdin"
        "   /dev/stdout"
        " | samtools sort"
        "   -T ${{TMPDIR:-/tmp}}/{wildcards.sample}_sort_$RANDOM"
        "   --output-fmt bam,level=0"
        "   -@ {threads}"
        "   -m {params.mem}m" # multiplied by {threads}
        "   /dev/stdin"
        " | samtools markdup"
        "   -T ${{TMPDIR:-/tmp}}/{wildcards.sample}_markdup_$RANDOM"
        "   -s" # report stats
        "   -@ {threads}"
        "   --output-fmt bam,level=0"
        "   /dev/stdin"
        "   /dev/stdout"
        " 2>{output.markdup}"
        " | tee {output.bam}"
        " | samtools index - {output.bai}"
        " ) > {log.log} 2>&1"


rule samstats:
    input:
        "tmp/aln/{sample}.bam",
    output:
        "output/samstats/{sample}.txt",
    log:
        "output/log/samstats/{sample}.log",
    threads: 1
    resources:
        mem="2G"
    container: "docker://ghcr.io/kdm9/align:latest"
    shell:
        "(samtools stats -i 5000 -x {input} >{output}) >{log} 2>&1"


def multiqc_input(wc):
    res = []
    for sample in config["samples"]:
        res.append(f"output/log/fastp/{sample}.json")
        if config.get("reference"):
            res.append(f"output/log/minimap/{sample}.log")
            res.append(f"output/log/markdup/{sample}.log")
            res.append(f"output/samstats/{sample}.txt")
        if config.get("kraken_db"):
            res.append(f"output/taxonid/kraken_report/{sample}.txt")
    return res

rule multiqc:
    input: multiqc_input
    output:
        html="output/multiqc.html",
    log:
        "output/multiqc.log",
    container: "docker://multiqc/multiqc:v1.25"
    shell:
        "multiqc"
        "   --no-megaqc-upload"
        "   --interactive"
        "   --no-data-dir"
        "   --filename {output.html}"
        "   {input}"
        " >{log} 2>&1"


rule mash_sketch_set:
    input:
        expand("tmp/reads/qc/{sample}_qc_R1.fastq", sample=config["samples"]),
    output:
        "tmp/mash/sketch.msh"
    log: "tmp/mash/sketch.msh.log"
    container: "docker://ghcr.io/kdm9/mash:latest"
    threads: 16
    resources:
        mem="16G"
    params:
        ksize=21,
        sketchsize=10000,
    shell:
        " mash sketch"
        "   -k {params.ksize}"
        "   -s {params.sketchsize}"
        "   -p {threads}"
        "   -o {output}"
        "   {input}"
        " >{log} 2>&1"


rule mash_dist_set:
    input:
        "tmp/mash/sketch.msh"
    output:
        "output/mash.dist.tsv"
    log:
        "output/mash.dist.tsv.log"
    container: "docker://ghcr.io/kdm9/mash:latest"
    threads: 16
    resources:
        mem="16G"
    shell:
        "mash dist"
        "   -p {threads}"
        "   -t" # tabular format
        "   {input} {input}" # needs input twice
        " >{output}"
        " 2>{log}"

"""
######################################### END SNAKEFILE #######################################################


def existing_path(arg):
    """Parse a CLI argument that must exist to a pathlib.Path (can be dir or file, so long as it exists)"""
    p = Path(arg)
    if not p.exists():
        raise ValueError(f"{arg} does not exist")
    return p.absolute()


class PathJsonEncoder(json.JSONEncoder):
    """A JSONEncoder that knows about Paths"""
    def default(self, o):
        import pathlib
        if isinstance(o, pathlib.PurePath):
            return str(o)
        return super().default(o)


def fastq_pairer(fastqs):
    samples = defaultdict(dict)
    for fastq in fastqs:
        fn = Path(fastq).name
        fn = re.sub(r"(\.fq|\.fastq)(\.(gz|bz2|zstd|zst))?", "", fn)
        m = re.search(r"(.+)_(R[12])(?=(_001|$))", fn)
        key = fn
        if not m:
            read = "??"
            print(f"WARNING: fastq with unknown pairing. This program assumes raw, paired end illumina data! ({fastq})", file=stderr)
        else:
            key = m.group(1)
            read = m.group(2)
        samples[key][read] = fastq
    return dict(samples.items())


def main(argv=None):
    ap = argparse.ArgumentParser()
    ap.add_argument("--reference", "-r", type=existing_path,
        help="Reference Genome")
    ap.add_argument("--seed", "-S", type=int, default=31912342,
        help="Random Seed for subsampling")
    ap.add_argument("--mash", "-m", action="store_true",
        help="Run mash?")
    ap.add_argument("--subsample", "-s", type=int, default=0,
        help="Number of reads to randomly subsample (0 to disable, which is the default)")
    ap.add_argument("--head", "-H", type=int, default=0,
        help="Number of reads take from head of file")
    ap.add_argument("--kraken-db", "-k", type=existing_path,
        help="Run Kraken + Bracken, using this DB (give dir name)")
    ap.add_argument("--workdir", type=Path, default=Path("seqchk.out"),
        help="Working dir")
    ap.add_argument("fastqs", type=existing_path, nargs="+")

    args = ap.parse_args(argv)
    if args.subsample > 0 and args.head > 0:
        print("ERROR: can only give one of --subsample and --head", file=stderr)
        raise RuntimeError()
    args.workdir.mkdir(exist_ok=True)
    with open(args.workdir / "config.json", "w") as fh:
        json.dump({
            "samples": fastq_pairer(args.fastqs),
            "reference": args.reference,
            "subsample": args.subsample,
            "head": args.head,
            "subsample_seed": args.seed,
            "kraken_db": args.kraken_db,
        }, fh, indent=4, cls=PathJsonEncoder)

    with open(args.workdir / "Snakefile", "w") as fh:
        fh.write(SNAKEFILE)

    with open(args.workdir / ".gitignore", "w") as fh:
        print("tmp", file=fh)
        print("output", file=fh)
        print(".snakemake", file=fh)

    print(f"Workflow written to {args.workdir.resolve()}\nTo execute, please run `snakemake` from there, with your profile or any of the usual arguments needed to run snakemake locally.")

if __name__ == "__main__":
    main()
